//==============================================================================
// ${COMMAND}

graph AdditiveSynth  [[ main ]]
{
    input event soul::midi::Message midiIn;
    output stream float audioOut;

    let
    {
        voices = Voice[8];
        voiceAllocator = soul::voice_allocators::Basic(8);
    }

    connection
    {
        midiIn -> soul::midi::MPEParser -> voiceAllocator;

        // Plumb the voice allocator to the voices array
        voiceAllocator.voiceEventOut -> voices.noteOn,
                                        voices.noteOff,
                                        voices.pitchBend;

        // Sum the voices audio out to the output
        voices       -> audioOut;
    }
}


//==============================================================================
graph Voice
{
    input event
    {
        soul::note_events::NoteOn noteOn;
        soul::note_events::NoteOff noteOff;
        soul::note_events::PitchBend pitchBend;
    }

    output stream float audioOut;

    let
    {
        osc             = OscillatorBank (${HARMONICS});
        amplitudeSource = AmplitudeSource (${HARMONICS}, ${FRAMES_BETWEEN_SAMPLES});
        declickEnvelope = soul::envelope::FixedAttackReleaseEnvelope (0.2f, 0.02f, 0.1f);
        attenuator      = soul::gain::DynamicGain (float);
    }

    connection
    {
        noteOn          -> osc.noteOn, amplitudeSource.noteOn;
        pitchBend       -> osc.pitchBend;
        noteOn, noteOff -> declickEnvelope.noteIn;

        amplitudeSource.out         -> osc.amplitudes;
        osc.audioOut                -> attenuator.in;
        declickEnvelope.levelOut    -> attenuator.gain;
        attenuator                  -> audioOut;
    }
}


//==============================================================================
processor OscillatorBank (int size = 64)
{
    input event
    {
        soul::note_events::NoteOn noteOn;
        soul::note_events::PitchBend pitchBend;
    }

    input stream float<size> amplitudes;

    output stream float audioOut;

    event noteOn (soul::note_events::NoteOn e)
    {
        notePitch = e.note;
        bendSemitones = 0.0f;
        oscillators = 1.0f;
        setMultipliers();
    }

    event pitchBend (soul::note_events::PitchBend e)
    {
        bendSemitones = e.bendSemitones;
        setMultipliers();
    }

    float notePitch, bendSemitones;

    void setMultipliers()
    {
        let noteFrequency  = soul::noteNumberToFrequency (notePitch + bendSemitones);

        let nyquist = float (processor.frequency / 2.0);

        for (wrap<size> i)
        {
            let harmonicFrequency = noteFrequency * (int (i) + 1);

            if (harmonicFrequency < nyquist)
            {
                let angle = float32 (twoPi * harmonicFrequency * processor.period);
                multiplier[i].real = cos (angle);
                multiplier[i].imag = sin (angle);
            }
            else
            {
                multiplier[i] = 1.0f;
            }
        }
    }

    complex32<size> oscillators = 1.0f, multiplier = 1.0f;

    void run()
    {
        loop
        {
            oscillators = oscillators * multiplier;

            if const (size == 1)
                audioOut << oscillators.imag * amplitudes;
            else
                audioOut << sum (oscillators.imag * amplitudes);

            advance();
        }
    }
}


//==============================================================================
processor AmplitudeSource (int size = 64, int stepRate = 1024)
{
    input event soul::note_events::NoteOn noteOn;
    output stream float<size> out;

    float<size>[] amplitudes = (
${AMPLITUDES}
    );

    event noteOn (soul::note_events::NoteOn e)
    {
        value = amplitudes[0];
        increment = (amplitudes[1] - amplitudes[0]) / float<size> (stepRate);
        nextSlot = 1;
        steps = stepRate;
    }

    void next()
    {
        if (nextSlot < amplitudes.size - 1)
        {
            value = amplitudes.at (nextSlot);
            increment = (amplitudes.at (nextSlot + 1) - amplitudes.at (nextSlot)) / float<size> (stepRate);
            nextSlot++;
            steps = stepRate;
        }
        else
        {
            increment = 0;
            steps = stepRate;
        }
    }

    float<size> value, increment;
    int nextSlot, steps;

    void run()
    {
        loop
        {
            out << value;
            value += increment;
            steps--;

            if (steps == 0)
                next();

            advance();
        }
    }
}
